#pragma -newline -w 32000
#pragma -comment
% #pragma -raw


% Boo tokens
tokens 
    % Boo string forms - shortstrings are already captured by [stringlit] and [charlit]
    % All may be preceded by a stringprefix, see [stringliteral] below
    longstringlit        "\"\"\"#[(\"\"\")]*\"\"\""
    longcharlit          "'''#[(''')]*'''"

    hexinteger   "0[Xx][\dABCDEFabcdef]+" 
    pointfloat   "\d*.\d+([Ee][+-]?\d+)?f?"        % other forms covered by [floatnumber]
                 | "\d+.\d*([Ee][+-]?\d+)?f?"
                 | "\d+f"
end tokens

keys 
    and       from      'not       while 
    elif      global    or        of
    'assert    else      if        pass      yield 
    break     except    'import    'print 
    class     exec      in        raise     callable
    continue  finally   is        return 
    def       for       lambda    try       @ 
    as        with      namespace elif      struct
    constructor         get       set       pass
    null      override  enum      isa       virtual
    abstract  self
end keys

compounds
    == != >= <=
    += -= *= /=
    ++ -- << >>
    ** =~ |= &=
end compounds

comments
    '#
    '//
    /* */
end comments

define program
    [class_file]
end define

define class_file
	[opt namespace_stmt_newline] [class_file_content]
end define

define class_file_content
    [repeat endofline] [repeat import_stmt_newline] [class_or_struct_or_interface_definition+]
end define

define namespace_stmt
	'namespace [id_with_dots] [opt ';]
end define

define namespace_stmt_newline
    [namespace_stmt] [endofline]
end define

define import_stmt_newline
    'import [id_with_dots] [opt import_alias] [opt ';] [repeat endofline]
end define

define import_alias
    'as [id]
end define

define class_or_struct_or_interface_definition
    [class_definition] | [struct_definition] | [interface_definition]
end define

define class_definition
    [repeat attribute_stmt_newline] [opt scope_modifier] [opt 'abstract] 'class [id] [opt generic_type_declaration] [opt derive] [class_body]
end define

define class_body
        [indent] [repeat class_member_newline] [dedent] [repeat endofline]
    |   [indent] 'pass [repeat endofline] [dedent] [repeat endofline]
end define

define interface_body
    [indent] [repeat function_declaration] [dedent] [repeat endofline]
end define

define struct_definition
    [opt scope_modifier] 'struct [id] [class_body]
end define

define interface_definition
    [opt scope_modifier] 'interface [id] [interface_body]
end define

define enum_definition
    [scope_modifier] 'enum [id] [enum_body] [repeat endofline]
end define

define enum_body
    [indent] [enum_item+] [dedent]
end define

define enum_item
    [id] [endofline+]
end define

define derive
    '( [type_base] ')
end define

define constraint
    '( [type_base] ')
end define

define class_member_newline
        [repeat attribute_stmt_newline] [class_member] [repeat endofline]
    |   [repeat attribute_stmt_newline] [class_definition]
    |   [repeat attribute_stmt_newline] [struct_definition]
    |   [interface_definition]
    |   [enum_definition]
end define

define class_member
        [class_member_function_definition]
    |   [class_member_property_definition]
    |   [class_member_variable_declaration]
    |   [callable_declaration]
end define

define class_member_variable_declaration
    [modifier] [variable_declaration] [opt variable_initialization] [opt ';]
end define

define class_member_property_definition
    [modifier] [opt 'override] [variable_declaration] [indent] [opt getter] [opt setter] [dedent]
end define

define getter
    'get [indent] [function_body] [dedent] [repeat endofline]
end define

define setter
    'set [indent] [function_body] [dedent] [repeat endofline]
end define

define class_member_function_definition
    [modifier] [opt virtuality] [function_header] [indent] [function_body] [dedent]
end define

define function_declaration
    [function_header] [indent] 'pass [endofline] [dedent] [repeat endofline]
end define

define callable_declaration
    [opt modifier] 'callable [id] [opt generic_type] '( [variable_declaration,] ') [type_declaration]
end define

define attribute_stmt_newline
    '[ [_inside_attribute] '] [repeat endofline]
end define

define _inside_attribute
    [function_call] | [leftValue]
end define

define function_header
        'def [id] [opt generic_type_declaration] '( [variable_declaration,] ') [opt type_declaration]
    |   [constructor_header]
end define

define constructor_header
    'def 'constructor '( [variable_declaration,] ')
end define

define function_body
    [repeat stmt_newline]
end define

define stmt_newline
        [single_stmt] [delimiter] [repeat endofline]
    |   [group_stmt]
end define

define delimiter
    '; | [opt ';] [endofline]
end define

define single_stmt
        [assignment_stmt]
    |   [function_call]
    |   [local_variable_definition]
    |   [return_stmt]
    |   [yield_stmt]
    |   'break
    |   'continue
    |   'pass
    |   [primary] '++
    |   [primary] '--
end define

define group_stmt
        [if_stmt]
    |   [for_in_stmt]
    |   [while_stmt]
    |   [local_function_definition]
end define

define assignment_stmt
    [leftValue] [assignment_op] [expression]
end define

define local_variable_definition
    [id] as [type] [opt variable_initialization]
end define

define variable_initialization
    '= [expression]
end define

define return_stmt
    'return [opt expression]
end define

define yield_stmt
    'yield [opt expression]
end define

define if_stmt
        [if_header] [if_body] [repeat endofline] [opt else_stmt]
    |   [single_stmt] [if_header] [opt ';] [repeat endofline+]
end define

define if_header
    'if [expression]
end define

define if_body
        [indent] [repeat stmt_newline] [dedent]
    |   ': [single_stmt]
end define

define else_stmt
        [elif_header] [indent] [repeat stmt_newline] [dedent] [repeat endofline] [opt else_stmt]
    |   'else [if_body] [repeat endofline]
end define

define elif_header
    'elif [expression]
end define

define for_in_stmt
    'for [id] [opt type_declaration] 'in [expression] [indent] [repeat stmt_newline] [dedent] [repeat endofline]
end define

define while_stmt
    [while_header] [indent] [repeat stmt_newline] [dedent] [repeat endofline]
end define

define while_header
    'while [expression]
end define

define local_function_definition
    [function_header] [indent] [function_body] [dedent] [repeat endofline]
end define

%

define function_call
    [objvalue] [opt generic_type] '( [expression,] ')
end define

define variable_declaration
    [id] 'as [type]
end define

define variable_optional_declaration
    [id] [opt type_declaration]
end define

define type_declaration
    'as [type]
end define

define modifier
    [opt 'static] [opt scope_modifier]
end define

define generic_type
    '[ 'of [type,] ']
end define

define generic_type_declaration
    '[ 'of [id] [opt constraint] ']
end define

define type
        [id_with_dots] [opt generic_type] [opt '?]
    |   [array_type]
    |   callable
end define

define array_type
        '( [type] ')
    |   '( [type] ', [number] )
end define

define type_base
        'class
    |   [id_with_dots]
end define

define scope_modifier
    'public | 'protected | 'private
end define

define virtuality
    'abstract | 'virtual | 'override
end define

define lambda_function_literal
    '{ [variable_optional_declaration,] '| [expression] '}
end define

%

define expression
        [primary]
    |   [expression] [dual_op] [expression]
    |   [una_op] [primary]
    |   [primary] '++
    |   [primary] '--
    |   [expression] 'as [id_with_dots]
    |   [expression] 'cast [id_with_dots]
end define

define primary
        [literal]
    |   [objvalue]
end define

define objvalue
        [string]
    |   [arraylit]
    |   'self
    |   '( [expression] ')
    |   [objvalue] '[ [expression,] ']
    |   [objvalue] '[ [expression] ': [opt expression] ']
    |   [objvalue] '[ ': [expression] ']
    |   [function_call]
    |   [objvalue] '. [id]
    |   [lambda_function_literal]
    |   [id]
end define

define literal
    [number] | 'true | 'false | [pointfloat] | null | [hexinteger]
end define

define id_with_dots
        [id]
    |   [id] '. [id_with_dots]
end define

define dual_op
        '==
    |   '!=
    |   'and
    |   'or
    |   '+
    |   '-
    |   '/
    |   '*
    |   '%
    |   '<
    |   '>
    |   '<=
    |   '>=
    |   '>>
    |   '<<
    |   '|
    |   'in
    |   '^
    |   '**
    |   '=~
    |   'isa
end define

define una_op
        '-
    |   'not
end define

define assignment_op
        '=
    |   '+=
    |   '-=
    |   '*=
    |   '/=
    |   '|=
    |   '&=
end define

define leftValue
    [objvalue]
end define

define string
        [stringlit] 
    |   [charlit] 
    |   [longstringlit] 
    |   [longcharlit] 
end define

define arraylit
        '( [expression] ', [expression,]')
    |   '( ', ')
end define
%

define indent
    ':[endofline] '{ [IN] [repeat endofline]
end define

define dedent
    [EX] '}
end define

define endofline
	[opt comment] [newline]
end define
